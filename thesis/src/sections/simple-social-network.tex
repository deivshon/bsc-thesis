\section{Simple Social Network}
The "Simple Social Network" app is a small web application that allows users to:
\begin{itemize}
    \item Create and log in to different accounts
    \item Create textual posts
    \item Like posts
    \item See recent posts in the homepage
    \item See recent user specific posts in user pages
\end{itemize}
This allows to explore what patterns can be used to handle different routes within the same SPA and interoperate with JavaScript

\subsection{URLs handling}
Contrary the the previous section's \textit{To do} app, within this application we need to handle different routes. While some frontend frameworks leave routing to third party libraries, Elm has a standardized way to deal with this ubiquitous problem.\\

Typically, within the \texttt{Model} of a routed Elm SPA a \texttt{page} field contains the current page type and the related state needed. For this application we only have four pages:
\begin{minted}{elm}
type Page
    = Login
    | Home
    | UserPage String
    | NotFound
\end{minted}
The login and home page don't need any extra state to be properly handled, while, since user pages are user specific, a \texttt{String} containing the related user ID is needed to then fetch the proper user posts.

The \texttt{main} function for this section's application looks like this

\begin{minted}{elm}
main =
    Browser.application
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        , onUrlRequest = LinkClicked
        , onUrlChange = UrlChanged
        }
\end{minted}

\texttt{onUrlChange} and \texttt{onUrlRequest} are the fields that allow to setup routing handling.\\

\texttt{onUrlRequest} accepts a message constructor that takes in a \texttt{Browser.UrlRequest} and \texttt{onUrlChange} accepts a message constructor that takes in a \texttt{Url.Url}, in our case, respectively, \texttt{LinkClicked} and \texttt{UrlChanged}.\\
\texttt{onUrlRequest} gets triggered when a link is clicked, made possible by Elm's internals which intercept navigations, and \texttt{onUrlChange} is instead triggered when a new internal URL is pushed through \texttt{Nav.pushUrl}.\\
Like all other messages, we can handle both within the \texttt{update} function.\\

While a link click can be handled any way that is needed, this is the typical way to do so in standard SPAs if one does not need to prevent navigation to certain URLs before doing some action first\cite{noauthor_navigation_nodate}:
\begin{minted}{elm}
case msg of
    LinkClicked urlRequest ->
        case urlRequest of
            Browser.Internal url ->
                ( model, Nav.pushUrl model.key (Url.toString url) )

            Browser.External href ->
                ( model, Nav.load href )
\end{minted}

Within the \texttt{LinkClicked} branch of the \texttt{msg} matching, we check if the link is external or internal, and either, respectively, directly load the external link or push the new internal one, thereby triggering a \texttt{UrlChanged} message in this last case.\\
When this new message is intercepted, we now need to handle the actual internal route change:
\begin{minted}{elm}
    case msg of
[...]
        UrlChanged url ->
            let
                newPage =
                    routeUrl url
            in
[...]
routeUrl url =
    Maybe.withDefault NotFound (Parser.parse routeParser url)
    
routeParser =
    oneOf
        [ Parser.map Home Parser.top
        , Parser.map Login (Parser.s "login")
        , Parser.map UserPage (Parser.s "users" </> Parser.string)
        ]
\end{minted}
The page the model should contain after the URL change is computed using  the official \texttt{URL} package's \texttt{Parser} module. \texttt{Home} corresponds to the root URL, \texttt{Login} to the \texttt{/login} route and \texttt{UserPage} to any route of the format \texttt{/users/:userId}.\\
Using this simple helper function, we can compute the new page in the \texttt{UrlChanged} branch of the \texttt{update} function, and, if needed, change the application's state based on it.

% ? \subsection{Handling state on URLs change}

\subsection{Using ports to access local storage}
% Mention that existing solution like localstorage packages still rely on ports

Another aspect of this section's app that was not present in the previous one is client-side data retention. If a user logs in the application, they should be able to stay logged in after the tab and browser session is closed. Since this is not a real world application but a sample one, authentication was implemented by including the header \texttt{X-User-Header} with the user ID as value in the API calls that need it.\\
In applications that run in the browser, data retention is typically achieved through the \texttt{localStorage} web API\cite{noauthor_window_nodate}, which allows developers to set and get values from a key/value store. Since Elm does not allow to directly call JavaScript functions and does not provide a first-party package to interact with the native \texttt{localStorage} API, it's necessary to use the standard way to interoperate with JavaScript from within the Elm runtime: ports and flags.

In the \texttt{Ports.elm} file we declare the ports:
\begin{minted}{elm}
port storeToken : String -> Cmd msg
port removeToken : () -> Cmd msg
port noInteractionTokenChange : (Maybe String -> msg) -> Sub msg
\end{minted}

Three ports exist, all concerning events related to the authentication token:
\begin{itemize}
    \item \texttt{storeToken} is used when after a successful login response
    \item \texttt{removeToken} is used after a logout
    \item \texttt{noInteractionTokenChange} is used when the token changes but the change was not triggered by some action in the current tab (this can happen if the user is using the application in multiple tabs) 
\end{itemize}
Ports allows message passing from JavaScript to Elm and viceversa, while flags allow proper initialization of state that is controlled outside of the Elm runtime, such as the authorization token in this case.\\

The \texttt{init} function's signature of this section's app includes a \texttt{Flags} type, containing the current authorization token at the start of the Elm runtime. 
\begin{minted}{elm}
type alias Flags =
    { authToken : Maybe String
    }
[...]
init : Flags -> Url.Url -> Nav.Key -> ( Model, Cmd Msg )
init flags url key =
[...]
\end{minted}

This way we always know that within Elm we're always starting the application with whatever token the user currently holds in their browser's \texttt{localStorage}.\\

Interactions with the token are achieved through ports. The use of ports on the JavaScript side is straightforward: a \texttt{ports} field is available in the \texttt{ElmApp} that is instantiated to start the Elm runtime:
\begin{minted}{js}
app.ports.storeToken?.subscribe((t) => {
    authToken.set(t);
});
\end{minted}
On the Elm side, ports that allow message passing from Elm to JavaScript are called like normal functions. The code below contains the commands returned in the \texttt{update} function following a successful login:
\begin{minted}{elm}
Cmd.batch
    [ storeToken user.id
    , Nav.pushUrl model.key "/"
    , Api.getPosts Pagination.defaultPaginationData user.id Nothing postsToMsg
    ]
\end{minted}
Calling \texttt{storeToken} triggers the callback that uses the \texttt{localStorage} API on the JavaScript side.

JavaScript to Elm ports are associated with a message through the \texttt{subscriptions} field of the \texttt{main} function.
\begin{minted}{elm}
subscriptions _ =
    noInteractionTokenChange TokenChangedInOtherTab
\end{minted}
In this case we'are associating the \texttt{noInteractionTokenChange} port JavaScript message to the internal \texttt{TokenChangedInOtherTab} message, which can be handled however the application needs to in the \texttt{update} function


\subsection{Login/Signup}
% mention the fact that login could have easily been a component in some other framework

The \texttt{Login} page that was shown before contains the login form, which means it must handle updates to the input data and trigger the proper API call once the user submits said data. The \texttt{Main.elm} file coordinates all state updates and commands, and the only action that we need to handle explicitly there is the submit action, the rest (updates to the current username/password input data) would only be overhead.\\
It therefore makes sense to create a new module that will contain the types, updates, messages and logic strictly related to the login data. We start by defining \texttt{UserAuthData} and \texttt{UserAuthMsg}:
\begin{minted}{elm}
type alias UserAuthData =
    { username : String
    , password : String
    , error : Bool
    }

type UserAuthMsg
    = UsernameChange String
    | PasswordChange String
    | Submit
\end{minted}

Within the same module, we can then create the view and update function related to this type and message:
\begin{minted}{elm}
update : UserAuthMsg -> UserAuthData -> UserAuthData
update msg userAuthData =
[...]
viewUserAuth : String -> UserAuthData -> (UserAuthMsg -> msg) -> H.Html msg
viewUserAuth label userAuthData onAction =
\end{minted}

By exposing these functions and types, we can then easily use this data within our \texttt{Main.elm} file.\\
Specifically, a field of \texttt{UserAuthData} type is going to be included in the model, and a \texttt{Msg} of type \texttt{UserAuthMsg}, while the related helper functions are going to be used within the \texttt{update} and view functions.\\

This powerful pattern allows us to reuse the module's functions for different instances of the same data type. In fact, in this section's application the sign in data is the exact same as the login data, a simple username and password form.\\ Since we already have all the helper functions needed, we just need to include another \texttt{UserAuthData} field in our model and another \texttt{UserAuthMsg}  \texttt{Msg}, this way we can use the same \texttt{UserAuth.update} function when receiving the different login and sign up related messages:
\begin{minted}{elm}  
type alias Model =
    [...]
    , loginData : UserAuth.UserAuthData
    , signupData : UserAuth.UserAuthData
    [...]
    }
[...]
type Msg
    [...]
    | UserAuthMsg UserAuth.UserAuthMsg
    | SignupMsg UserAuth.UserAuthMsg
    [...]
\end{minted}

While the \texttt{UserAuth} module is not a component, the only thing differentiating it from one is the need to specify the state and messages explicitly in the \texttt{Main.elm} module, instead of encapsulating them completely and allow message passing through other means. Nonetheless, this pattern allowed the sign up form to be seamlessly created reusing existing types and functions.